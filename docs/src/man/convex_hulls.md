# Convex Hulls

In this section we illustrate the
[convex hull operation](https://en.wikipedia.org/wiki/Convex_hull).
We give examples of the symbolic implementation, and the concrete convex hull in
low dimensions.

```@contents
Pages = ["convex_hulls.md"]
Depth = 3
```

```@meta
DocTestSetup = quote
    using LazySets, Plots, LazySets.Approximations
end
```

## Symbolic convex hull

The lazy convex hull, `ConvexHull`, is the binary operator that implements the
convex hull of the union between two sets.

```@example example_ch
using LazySets, Plots

A = 1/sqrt(2.) * [1 -1; 1 1]
Bn = n -> BallInf(ones(n), 0.2)

X = Bn(2)
Y = CH(X, expm(A) * X)
```

The name `CH` is an alias for `ConvexHull`, so you can use both interchangeably.
This type is parametric in the operands's types.

```@example example_ch
p = plot(X, 1e-2, color="blue")
plot!(p, expm(A) * X, 1e-2, color="green")
plot!(p, Y, 1e-2, color="red", alpha=0.2)
```

We can as well work with a 100-dimensional set:

```@example example_ch
X = Bn(100)
A = blkdiag([sparse(A) for i in 1:50]...)
Y = CH(X, expm(full(A)) * X)

dim(Y)
```

To take the convex hull of a large number of sets, there is the `n`-ary type
`ConvexHullArray`. For instance, below we create a collection of balls `b` via
list comprehension, and pass them to create a new `ConvexHullArray` instance.

```@example example_ch
b = [Ball2([2*pi*i/100, sin(2*pi*i/100)], 0.05) for i in 1:100];
c = ConvexHullArray(b);

plot(c, 1e-3, alpha=0.1, color="blue")
plot!(b, 1e-3, alpha=0.5, color="red")
```

## 2D convex hull

In two dimensions the `convex_hull` function computes the concrete convex hull
of a set of points.

```@example example_ch
points = N -> [randn(2) for i in 1:N]
v = points(30)
hull = convex_hull(v)
typeof(hull), length(v), length(hull)
```

Notice that the output is a vector of floating point numbers representing the
coordinates of the points, and that the number of points in the convex hull has
decreased.

We can plot both the random points and the polygon generated by the convex hull
with the `plot` function:

```@example example_ch
p = plot([Singleton(vi) for vi in v])
plot!(p, VPolygon(hull), alpha=0.2)
```

## Using static vectors

Usual vectors are such that you can `push!` and `pop!` without changing its
type: the size is not a static property.
Vectors of fixed size, among other types, are provided by the
[StaticArrays.jl](https://github.com/JuliaArrays/StaticArrays.jl) package from
the [JuliaArrays](https://github.com/JuliaArrays) ecosystem.
Using static arrays for vectors of "small" dimension can dramatically improve
performance.

Since the convex hull algorithm supports any `AbstractVector`, it can be applied
with static vectors. The following example illustrates this fact.


```@example example_ch
v = points(1000)
convex_hull(points(3)) # warm-up

@time convex_hull(v)
```

Now working with static vectors:

```@example example_ch
using StaticArrays

convex_hull([@SVector(rand(2)) for i in 1:3]) # warm-up

v_static = [SVector{2, Float64}(vi) for vi in v]
@time convex_hull(v_static)
```
