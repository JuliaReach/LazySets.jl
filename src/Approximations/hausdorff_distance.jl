export hausdorff_distance

"""
    hausdorff_distance(X::LazySet{N}, Y::LazySet{N}; [p]::N=N(Inf),
                       [Îµ]::N=N(1e-3)) where {N<:Real}

Compute the Hausdorff distance between two convex sets up to a given threshold.

### Input

- `X` -- convex set
- `Y` -- convex set
- `p` -- (optional, default: `Inf`) norm parameter of the Hausdorff distance
- `Îµ` -- (optional, default: `1e-3`) precision threshold; the true Hausdorff
         distance may diverge from the result by at most this value

### Output

A value from the ``Îµ``-neighborhood of the Hausdorff distance between ``X`` and
``Y``.

### Notes

Given a ``p``-norm, the Hausdorff distance ``d_H^p(X, Y)`` between sets ``X``
and ``Y`` is defined as follows:

```math
    d_H^p(X, Y) = \\inf\\{Î´ â‰¥ 0 \\mid Y âŠ† X âŠ• Î´ ðµ_p^n \\text{ and } X âŠ† Y âŠ• Î´ ðµ_p^n\\}
```

Here ``ðµ_p^n`` is the ``n``-dimensional unit ball in the ``p``-norm.

The implementation may internally rely on the support function of ``X`` and
``Y``; hence any imprecision in the implementation of the support function may
affect the result.
At the time of writing, the only set type with imprecise support function is the
lazy [`Intersection`](@ref).

### Algorithm

We perform binary search for bounding the Hausdorff distance in an interval
``[l, u]``, where initially ``l`` is ``0`` and ``u`` is described below.
The binary search terminates when ``u - l â‰¤ Îµ``, i.e., the interval becomes
sufficiently small.

To find an upper bound ``u``, we start with the heuristics of taking the biggest
distance in the axis-parallel directions.
As long as this bound does not work, we increase the bound by ``2``.

Given a value ``Î´``, to check whether the sets are within Hausdorff distance
``Î´``, we simply check the inclusions given above, where on the right-hand side
we use a lazy `MinkowskiSum` with a `Ballp` centered in the origin.
"""
function hausdorff_distance(X::LazySet{N}, Y::LazySet{N}; p::N=N(Inf),
                            Îµ::N=N(1e-3)) where {N<:Real}
    @assert isbounded(X) && isbounded(Y) "the Hausdorff distance is only " *
        "defined for compact sets"

    n = dim(X)
    @assert dim(Y) == n "the Hausdorff distance is only defined between sets " *
                        "of the same dimension, but they had dimensions $n " *
                        "resp. $(dim(Y))"

    # phase 1: find a finite upper bound
    Î´_upper = maximum(d -> abs(Ï(d, X) - Ï(d, Y)), BoxDirections{N}(n))
    # verify that this is an upper bound
    while !_mutual_issubset_in_Î´_bloating(X, Y, Î´_upper, n, p)
        Î´_upper *= N(2)
    end

    # phase 2: perform binary search between lower bound (initially 0) and upper
    # bound until convergence
    Î´_lower = N(0)
    while Î´_upper - Î´_lower > Îµ
        Î´ = (Î´_upper + Î´_lower) / N(2)
        if _mutual_issubset_in_Î´_bloating(X, Y, Î´, n, p)
            Î´_upper = Î´
        else
            Î´_lower = Î´
        end
    end
    return Î´_upper
end

function _mutual_issubset_in_Î´_bloating(X, Y, Î´, n, p)
    return _issubset_in_Î´_bloating(X, Y, Î´, n, p) &&
           _issubset_in_Î´_bloating(Y, X, Î´, n, p)
end

function _issubset_in_Î´_bloating(X::LazySet{N}, Y, Î´, n, p) where {N}
    return X âŠ† Y + Ballp(p, zeros(N, n), Î´)
end

# for polytopes the default implementation of `âŠ†` requires membership in the rhs
# set, which will be a MinkowskiSum and hence not available; we use the
# alternative based on constraints_list on the right instead
function _issubset_in_Î´_bloating(X::AbstractPolytope{N}, Y, Î´, n, p
                                ) where {N<:Real}
    return LazySets._issubset_constraints_list(X, Y + Ballp(p, zeros(N, n), Î´))
end


